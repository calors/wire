plugins {
    id 'base'
    id 'java-library'
    id 'eclipse'
    id 'com.google.protobuf' version '0.8.12'
    id 'com.bmuschko.nexus' version '2.3.1'
}
apply from: 'gradle/eclipse-prefs/eclipse.gradle'

repositories {
    mavenCentral()
}

if (project.hasProperty('enableJaCoCo')) {
    apply plugin: 'jacoco'
    jacoco { toolVersion = '0.8.5' }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation 'com.google.protobuf:protobuf-java:3.11.4'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.ofmooseandmen:halo:2018.03.1'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.6.1'
    testImplementation 'io.cucumber:cucumber-java:5.6.0'
    testImplementation 'io.cucumber:cucumber-java8:5.6.0'
    testImplementation 'io.cucumber:cucumber-junit:5.6.0'
    testImplementation 'io.cucumber:cucumber-picocontainer:5.6.0'
    testImplementation 'org.awaitility:awaitility:4.0.2'
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
}

test {
    useJUnit()
    forkEvery = 1
    testLogging.showStandardStreams = true
}

if (project.hasProperty('enableJaCoCo')) {
    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
        }
    }
}

if (Integer.valueOf(JavaVersion.current().getMajorVersion()) > 8) {
    // --release 8: configures the compiler to produce class files that will link against version 8
    compileJava.options.compilerArgs += ['--release', 8]
}

compileJava.options.encoding = 'UTF-8'
// See: https://docs.oracle.com/javase/9/tools/javac.htm#JSWOR627
compileJava.options.compilerArgs += [
    // '-Xlint:cast', // FIXME because of protobuf
    '-Xlint:classfile',
    '-Xlint:deprecation',
    '-Xlint:dep-ann',
    '-Xlint:divzero',
    '-Xlint:empty',
    '-Xlint:fallthrough',
    '-Xlint:finally',
    '-Xlint:overrides',
    '-Xlint:path',
    '-Xlint:processing',
    '-Xlint:rawtypes',
    '-Xlint:serial',
    '-Xlint:static',
    '-Xlint:unchecked',
    '-Xlint:varargs',
    '-Xlint:-options',
    '-Werror'
]

jar {
    /* include source */
    from (sourceSets.main.allSource.getSrcDirTrees())
}

javadoc {
   /* exclude this public file generated by protobuf. */
   exclude '**/CastChannel.java'
   options.memberLevel = JavadocMemberLevel.PROTECTED
}

/* Modify the Eclipse project settings. */
eclipse { 
    jdt { file { withProperties eclipsePrefs } } 

    classpath {
        /* ignore optional problems in generated code. */
        file {
            whenMerged {
                entries.each {
                    source ->
                    if (source.kind == 'src' && source.path.contains('generated')) {
                        source.entryAttributes['ignore_optional_problems'] = 'true'
                    }
                }
            }
        }
    }
}

task ('copyEclipsePrefs', type: Copy) {
    from rootProject.file('gradle/eclipse-prefs/org.eclipse.jdt.ui.prefs')
    into "${projectDir}/.settings"
}
tasks.eclipse.finalizedBy copyEclipsePrefs

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

modifyPom {
    project {
        name 'wire'
        description 'An implementation of the Google CASTV2 protocol in Java.'
        url 'https://github.com/ofmooseandmen/wire'
        inceptionYear '2018'

        scm {
            url 'https://github.com/ofmooseandmen/wire'
            connection 'scm:https://ofmooseandmen@github.com/ofmooseandmen/wire.git'
            developerConnection 'scm:git://github.com/ofmooseandmen/wire.git'
        }

        licenses {
            license {
                name 'The 3-Clause BSD License'
                url 'https://opensource.org/licenses/BSD-3-Clause'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'ofmooseandmen'
                name 'Cedric Liegeois'
                email 'ofmooseandmen@yahoo.com'
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

wrapper {
    gradleVersion = '6.2'
    distributionType = Wrapper.DistributionType.BIN
}
